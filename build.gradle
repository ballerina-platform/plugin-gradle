plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id "net.researchgate.release" version '2.8.0'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
    }

    maven {
        url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

dependencies {
    implementation    gradleApi()
    testImplementation gradleTestKit()
}

gradlePlugin {
    plugins {
        BallerinaPlugin {
            id = 'io.ballerina.plugin'
            implementationClass = 'io.ballerina.plugin.BallerinaPlugin'
            displayName = 'Ballerina Plugin'
            description = 'A plugin for Ballerina projects'
            website = 'https://github.com/ballerina-platform/plugin-gradle'
            vcsUrl = 'https://github.com/ballerina-platform/plugin-gradle.git'
            tags.set(['Ballerina', 'Plugin', 'Gradle'])
        }
    }
}

def moduleVersion = project.version.replace("-SNAPSHOT", "")

release {
    failOnPublishNeeded = false
    failOnSnapshotDependencies = true

    buildTasks = ['build']
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v$version'

    git {
        requireBranch = "release-${moduleVersion}"
        pushToRemote = 'origin'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/plugin-gradle")
            credentials {
                username System.getenv("publishUser")
                password System.getenv("publishPAT")
            }
        }
    }
}

test {
    useJUnitPlatform()
}

publishToMavenLocal.dependsOn build
publish.dependsOn build
